apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'

wrapper {
    gradleVersion = '3.0'
}

repositories {
    jcenter()
}

group = "org.talend.buildtools"
version = "0.1.0-SNAPSHOT"


// Hack to make sure we preload the Groovy compiler to not have a Groovy version
// conflict when Spock is loaded.
configurations.all {
    resolutionStrategy {
        force 'org.codehaus.groovy:groovy-all:2.4.7'
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

publishing {
    repositories {
        // publish to a different repo based on snapshot suffix
        // but only if username/password for that repo is defined
        // so that it won't publish from local workstations
        // in which case it should publish to local maven
        if (version.endsWith('-SNAPSHOT')) {
            if (project.hasProperty('nexusOssSnapshotsUsername')) {
                maven {
                    url nexusOssSnapshotsUrl
                    credentials {
                        username nexusOssSnapshotsUsername
                        password nexusOssSnapshotsPassword
                    }
                }
            } else {
                mavenLocal()
            }
        } else {
            if(project.hasProperty('nexusOssReleasesUsername')) {
                maven {
                    url nexusOssReleasesUrl
                    credentials {
                        username nexusOssReleasesUsername
                        password nexusOssReleasesPassword
                    }
                }
            } else {
                mavenLocal()
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId "talend-gradle-root"
            version version
        }
    }
}

// Skip the publishes that happen other than the one we explicitly request (there should be a better way)
// http://stackoverflow.com/questions/21433251/gradle-plugin-maven-publish-how-to-publish-only-specific-publication-to-a-rep
afterEvaluate {
    tasks.withType(PublishToMavenRepository).all { publishTask ->
        publishTask.onlyIf { task ->
            task.name.startsWith("publishMaven")
        }
    }
}

println "${group}.${name}"
println "${group}.TalendRootPlugin"

// For plugin testing (used by java-gradle-plugin)
gradlePlugin {
    plugins {
        "talend-root" {
            id = "${group}.${name}"
            implementationClass = "${group}.TalendRootPlugin"
        }
    }
}



